//apply plugin: 'idea'
apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.1'

    defaultConfig {
        applicationId "org.bottiger.podcast"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 305        // even - release. Odd - development
        versionName "0.141" // even - release. Odd - development
        multiDexEnabled = true
        //renderscriptTargetApi 20
        //renderscriptSupportModeEnabled true

        vectorDrawables.useSupportLibrary = true

        // Espresso
        //testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        ndk {
            //moduleName "hello-jni"
            moduleName "sonic"
            //moduleName "swutils"
        }
    }

    dataBinding {
        enabled = true
    }

    // Point Gradle at the directory where you want to root all of your native code
    // There is a default directory but I prefer to control which directories to use
    // and there will be a point later where keeping control over this is important
    sourceSets.main {
        jni.srcDirs = ["src/main/jni"];
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'LICENSE.txt'
    }

    productFlavors {

        dev {
            buildConfigField "boolean", "DEV_MODE", "true"
            buildConfigField "boolean", "PRIVATE_MODE", "false"
            buildConfigField "boolean", "LIBRE_MODE", "true"
        }

        free {
            buildConfigField "boolean", "DEV_MODE", "false"
            buildConfigField "boolean", "PRIVATE_MODE", "true"
            buildConfigField "boolean", "LIBRE_MODE", "true"
        }

        google {
            buildConfigField "boolean", "DEV_MODE", "false"
            buildConfigField "boolean", "PRIVATE_MODE", "false"
            buildConfigField "boolean", "LIBRE_MODE", "false"
        }

        amazon {
            buildConfigField "boolean", "DEV_MODE", "false"
            buildConfigField "boolean", "PRIVATE_MODE", "false"
            buildConfigField "boolean", "LIBRE_MODE", "false"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // http://stackoverflow.com/questions/22851103/android-studio-gradle-error-multiple-dex-files-define
    dexOptions {
        maxProcessCount 4
        javaMaxHeapSize "2g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile 'com.android.support:multidex:1.0.1'

    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.4.1'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup:otto:1.3.8'
    compile('com.googlecode.json-simple:json-simple:1.1.1') {
        exclude module: 'junit'
    }
    //compile 'com.google.apis:google-api-services-drive:+'

    compile 'io.reactivex:rxandroid:1.2.1'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.reactivex:rxjava:1.1.6'
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'

    compile 'io.requery:sqlite-android:3.14.2'

    compile 'com.google.android.exoplayer:exoplayer:r2.0.2'

    compile 'com.wdullaer:materialdatetimepicker:2.5.0'

    // Glide
    compile 'com.github.bumptech.glide:glide:3.7.0'

    compile 'net.steamcrafted:materialiconlib:1.0.9'

    compile 'org.whispersystems:libpastelog:1.0.7'

    //compile 'com.google.api-client:google-api-client-android:+'
    compile 'com.google.http-client:google-http-client-gson:1.22.0'
    compile 'com.android.support:support-v4:24.2.1'
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:recyclerview-v7:24.2.1'
    compile 'com.android.support:palette-v7:24.2.1'
    compile 'com.android.support:cardview-v7:24.2.1'
    compile 'com.android.support:mediarouter-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.android.support:percent:24.2.1'

    compile 'jp.wasabeef:recyclerview-animators:2.2.3'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.1'
    compile 'com.nineoldandroids:library:2.4.0'

    compile 'com.bugsnag:bugsnag-android:3.5.0'

    compile 'com.journeyapps:zxing-android-embedded:3.3.0'

    // For testing only
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
    configurations.all {
        resolutionStrategy.force 'com.android.support:support-annotations:23.4.0'
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
    }

    // Espresso

    // https://developer.android.com/training/testing/ui-testing/espresso-testing.html
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2') {
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'recyclerview-v7'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    // Free version

    // Amazon version
    amazonCompile fileTree(dir: 'src/amazon/libs', include: ['*.jar'])
    amazonCompile 'com.google.android.gms:play-services-cast:9.6.1'
    // google cast

    // Play Store
    googleCompile 'com.google.android.gms:play-services-analytics:9.6.1'
    // analytics
    googleCompile 'com.google.android.gms:play-services-cast:9.6.1'
    googleCompile 'com.google.android.gms:play-services-cast-framework:9.6.1'
    googleCompile 'com.google.android.gms:play-services-auth:9.6.1'
    googleCompile 'com.google.firebase:firebase-messaging:9.6.1'

    // google cast
    compile project(path: ':WebPlayer', configuration: 'android-endpoints')
    compile project(':soundwavescommon')

}

task nonfreeReleaseTask << {
    apply plugin: 'com.google.gms.google-services'
}

tasks.whenTaskAdded { task ->
    if (task.name != 'free') {
        task.dependsOn nonfreeReleaseTask
    }
}

//apply plugin: 'com.google.gms.google-services'
